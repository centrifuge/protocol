// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.28;

import {ILockup} from "./interfaces/ILockup.sol";
import {IFreezable} from "./interfaces/IFreezable.sol";
import {IMemberlist} from "./interfaces/IMemberlist.sol";
import {UpdateRestrictionType, UpdateRestrictionMessageLib} from "./libraries/UpdateRestrictionMessageLib.sol";

import {Auth} from "../misc/Auth.sol";
import {CastLib} from "../misc/libraries/CastLib.sol";
import {BytesLib} from "../misc/libraries/BytesLib.sol";
import {IERC165} from "../misc/interfaces/IERC7575.sol";
import {BitmapLib} from "../misc/libraries/BitmapLib.sol";

import {IRoot} from "../common/interfaces/IRoot.sol";
import {ITransferHook, HookData, ESCROW_HOOK_ID} from "../common/interfaces/ITransferHook.sol";

import {IShareToken} from "../spoke/interfaces/IShareToken.sol";

/// @title  BaseTransferHook
/// @dev    Supports checking transfer types, storing memberlists for share tokens,
///         freezing and unfreezing users, and storing the last deposit claim date (used for lockup periods).
///
///         Hook data storage layout:
///          Bytes   Type     Description
///          -------------------------------------------------------
///          1-8     uint64   Memberlist valid until (unix timestamp)
///          9-13    uint32   Last deposit claim (unix timestamp)
///          14-15   -        -
///          15-16   bool     Whether the account is frozen
abstract contract BaseTransferHook is Auth, IMemberlist, IFreezable, ILockup, ITransferHook {
    using BitmapLib for *;
    using UpdateRestrictionMessageLib for *;
    using BytesLib for bytes;
    using CastLib for bytes32;

    error InvalidInputs();

    /// @dev Least significant bit
    uint8 public constant FREEZE_BIT = 0;

    IRoot public immutable root;
    address public immutable redeemSource;
    address public immutable depositTarget;
    address public immutable crosschainSource;

    constructor(
        address root_,
        address redeemSource_,
        address depositTarget_,
        address crosschainSource_,
        address deployer
    ) Auth(deployer) {
        require(
            redeemSource_ != depositTarget_ && depositTarget_ != crosschainSource_ && redeemSource_ != crosschainSource_,
            InvalidInputs()
        );

        root = IRoot(root_);
        redeemSource = redeemSource_;
        depositTarget = depositTarget_;
        crosschainSource = crosschainSource_;
    }

    //----------------------------------------------------------------------------------------------
    // Callback from share token
    //----------------------------------------------------------------------------------------------

    /// @inheritdoc ITransferHook
    function onERC20Transfer(address from, address to, uint256 value, HookData calldata hookData)
        external
        virtual
        returns (bytes4)
    {
        require(checkERC20Transfer(from, to, value, hookData), TransferBlocked());
        return ITransferHook.onERC20Transfer.selector;
    }

    /// @inheritdoc ITransferHook
    function onERC20AuthTransfer(
        address, /* sender */
        address, /* from */
        address, /* to */
        uint256, /* value */
        HookData calldata /* hookData */
    ) external pure virtual returns (bytes4) {
        return ITransferHook.onERC20AuthTransfer.selector;
    }

    function checkERC20Transfer(address from, address to, uint256, /* value */ HookData calldata hookData)
        public
        view
        virtual
        returns (bool);

    function isDepositRequest(address from, address to) public view returns (bool) {
        return from == address(0) && to != depositTarget;
    }

    function isDepositFulfillment(address from, address to) public view returns (bool) {
        return from == address(0) && to == depositTarget;
    }

    function isDepositClaim(address from, address to) public view returns (bool) {
        return from == depositTarget && to != address(0);
    }

    function isRedeemRequest(address, address to) public pure returns (bool) {
        return to == ESCROW_HOOK_ID;
    }

    function isRedeemFulfillment(address from, address to) public view returns (bool) {
        return from == redeemSource && to == address(0);
    }

    function isRedeemClaim(address from, address to) public view returns (bool) {
        return (from != redeemSource && from != crosschainSource) && to == address(0);
    }

    function isCrosschainTransfer(address from, address to) public view returns (bool) {
        return from == crosschainSource && to == address(0);
    }

    function isSourceOrTargetFrozen(address from, address to, HookData calldata hookData) public view returns (bool) {
        return (uint128(hookData.from).getBit(FREEZE_BIT) == true && !root.endorsed(from))
            || (uint128(hookData.to).getBit(FREEZE_BIT) == true && !root.endorsed(to));
    }

    function isSourceLocked(address from, HookData calldata hookData, uint32 lockupPeriod) public view returns (bool) {
        return uint32(uint128(hookData.from) >> 32) >= block.timestamp + lockupPeriod || root.endorsed(from);
    }

    function isTargetLocked(address to, HookData calldata hookData, uint32 lockupPeriod) public view returns (bool) {
        return uint32(uint128(hookData.to) >> 32) >= block.timestamp + lockupPeriod || root.endorsed(to);
    }

    function isSourceMember(address from, HookData calldata hookData) public view returns (bool) {
        return uint128(hookData.from) >> 64 >= block.timestamp || root.endorsed(from);
    }

    function isTargetMember(address to, HookData calldata hookData) public view returns (bool) {
        return uint128(hookData.to) >> 64 >= block.timestamp || root.endorsed(to);
    }

    //----------------------------------------------------------------------------------------------
    // Restriction updates
    //----------------------------------------------------------------------------------------------

    /// @inheritdoc ITransferHook
    function updateRestriction(address token, bytes memory payload) external virtual auth {
        UpdateRestrictionType updateId = payload.updateRestrictionType();

        if (updateId == UpdateRestrictionType.Member) {
            UpdateRestrictionMessageLib.UpdateRestrictionMember memory m = payload.deserializeUpdateRestrictionMember();
            updateMember(token, m.user.toAddress(), m.validUntil);
        } else if (updateId == UpdateRestrictionType.Freeze) {
            UpdateRestrictionMessageLib.UpdateRestrictionFreeze memory m = payload.deserializeUpdateRestrictionFreeze();
            freeze(token, m.user.toAddress());
        } else if (updateId == UpdateRestrictionType.Unfreeze) {
            UpdateRestrictionMessageLib.UpdateRestrictionUnfreeze memory m =
                payload.deserializeUpdateRestrictionUnfreeze();
            unfreeze(token, m.user.toAddress());
        } else {
            revert InvalidUpdate();
        }
    }

    /// @inheritdoc IFreezable
    function freeze(address token, address user) public auth {
        require(user != address(0), CannotFreezeZeroAddress());
        require(!root.endorsed(user), EndorsedUserCannotBeFrozen());

        uint128 hookData = uint128(IShareToken(token).hookDataOf(user));
        IShareToken(token).setHookData(user, bytes16(hookData.withBit(FREEZE_BIT, true)));

        emit Freeze(token, user);
    }

    /// @inheritdoc IFreezable
    function unfreeze(address token, address user) public auth {
        uint128 hookData = uint128(IShareToken(token).hookDataOf(user));
        IShareToken(token).setHookData(user, bytes16(hookData.withBit(FREEZE_BIT, false)));

        emit Unfreeze(token, user);
    }

    /// @inheritdoc IFreezable
    function isFrozen(address token, address user) public view returns (bool) {
        return uint128(IShareToken(token).hookDataOf(user)).getBit(FREEZE_BIT);
    }

    /// @inheritdoc IMemberlist
    function updateMember(address token, address user, uint64 validUntil) public auth {
        require(block.timestamp <= validUntil, InvalidValidUntil());
        require(!root.endorsed(user), EndorsedUserCannotBeUpdated());

        uint128 hookData = uint128(validUntil) << 64;
        hookData = hookData.withBit(FREEZE_BIT, isFrozen(token, user));
        IShareToken(token).setHookData(user, bytes16(hookData));

        emit UpdateMember(token, user, validUntil);
    }

    /// @inheritdoc IMemberlist
    function isMember(address token, address user) external view returns (bool isValid, uint64 validUntil) {
        validUntil = abi.encodePacked(IShareToken(token).hookDataOf(user)).toUint64(0);
        isValid = validUntil >= block.timestamp;
    }

    /// @inheritdoc ILockup
    function updateLastDepositClaim(address token, address user) public auth {
        uint128 hookData = uint128(IShareToken(token).hookDataOf(user));
        hookData = hookData | (uint128(uint32(block.timestamp)) << 32);
        IShareToken(token).setHookData(user, bytes16(hookData));

        emit UpdateLastDepositClaim(token, user, uint32(block.timestamp));
    }

    // TODO: updateLockupPeriod, isLocked

    //----------------------------------------------------------------------------------------------
    // ERC-165
    //----------------------------------------------------------------------------------------------

    /// @inheritdoc IERC165
    function supportsInterface(bytes4 interfaceId) external pure override returns (bool) {
        return interfaceId == type(ITransferHook).interfaceId || interfaceId == type(IERC165).interfaceId;
    }
}
